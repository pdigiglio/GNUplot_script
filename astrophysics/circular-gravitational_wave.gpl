
## @file circular-gravitational_wave.gpl
## @brief circular mass distribution whena a circular-polarized gravitationa wave 
## in Transverse-Traceless gauge passes by.

## @par wave parameters
## the wave propagates in the \f$x_3\f$ direction with 
## wave vector \f$k^\mu = (k,0,0,k)\f$ and amplitude \f$b\f$
k = 1
b = .5

set parametric
set dummy u,t

set urange [0:2*pi]
set trange [0:2*pi/k]

set isosamples 60, 60

# specify contour levels
set cntrparam levels discrete 0, .5 * pi / k, pi * k, 1.5 * pi / k
set contour

set title "circular polarized gravitational wave"

# this parameter sets the left/right handed polarization
# +1 --> left
# -1 --> right 
l = +1

set xlabel "distance along x (zeta_1)"
set ylabel "distance along y (zeta_2)"
set zlabel "time (x0)"

z0 = 0. # fix position of xy-plane

# define initial coordinate separations as parameter
Xi1( u, t ) = cos(u) - .5 * b * ( - cos( k *( t - z0 ) ) * cos(u) + l * sin( k *( t - z0 ) ) * sin(u))
Xi2( u, t ) = sin(u) - .5 * b * ( + l * sin( k *( t - z0 ) ) * cos(u) + cos( k *( t - z0 ) ) * sin(u) )

set hidden3d
set grid

## @par
## Here I plot the evolution for the entire distribution and the trajectories of four
## particles. The particles move on circles (which become helices when the time evolves)
splot Xi1(u, t), Xi2(u, t), t title "distribution evolution in time",\
	Xi1(0, t), Xi2(0, t), t notitle,\
	Xi1(.5*pi, t), Xi2(.5*pi, t), t notitle,\
	Xi1(pi, t), Xi2(pi, t), t notitle,\
	Xi1(1.5*pi, t), Xi2(1.5*pi, t), t notitle


# right handed circular polarized wave
#l = -1
#splot Xi1(u, t), Xi2(u, t), t
